apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
//        ndk {
//            moduleName "serial_port"
//            ldLibs "log", "z", "m"
//            abiFilters "armeabi", "armeabi-v7a", "x86"
//
//        }
    }
    sourceSets.main.jni.srcDirs = []
    //自定义Android.mk
    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
        //获取local.properties文件属性
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def ndkDir = properties.getProperty('ndk.dir')
        commandLine "$ndkDir/ndk-build",
                'NDK_PROJECT_PATH=build',
                'APP_BUILD_SCRIPT=src/main/jni/Android.mk'
//                'NDK_APPLICATION_MK=src/main/jni/Application.mk'
    }
    //设置编译目录
    task ndkLibsToJar(type: Zip, dependsOn: 'ndkBuild', description: 'Create a JAR of the native libs') {
        destinationDir new File(buildDir, 'libs')
        baseName 'ndk-libs'
        extension 'jar'
        from(new File(buildDir, 'libs')) { include '**/*.so' }
        into 'libs/'
    }
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkLibsToJar
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar', '*.so'])
    //加入自定义编译的so
    compile fileTree(dir: new File(buildDir, 'libs'), include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
}
